# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=filesystem
pkgver=1.0
pkgrel=1
pkgdesc="Filesystem layout and basic configuration files"
arch=('any')
url="https://www.limacon-audio.com/"
license=('MIT')

package() {
	cd $pkgdir

	for d in boot dev etc home mnt usr var opt srv/http run; do
		install -d -m755 $d
	done

	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	install -d -m555 -g 11 srv/ftp

	ln -sf ../proc/self/mounts etc/mtab

	for d in cache local opt log/old lib/misc empty; do
		install -d -m755 var/$d
	done
	install -d -m1777 var/{tmp,spool/mail}

	install -d -m775 -g 50 var/games
	ln -sf spool/mail var/mail
	ln -sf ../run var/run
	ln -sf ../run/lock var/lock

	for d in bin include lib share/misc src; do
		install -d -m755 usr/$d
	done

	ln -sf usr/lib lib
	ln -sf usr/bin bin
	ln -sf usr/bin sbin
	ln -sf bin usr/sbin

	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/$d
	done
}
